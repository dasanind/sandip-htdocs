<html>
<head>  
<title>A Beginner's Guide to Reasoning about Quantification in ACL2</title>
<body TEXT="#000000" 
      BGCOLOR="FFFFFF"
      LINK ="#FF0000"
      VLINK="#A020F0" 
      ALINK="#FFD700"
      fixed>

<H1><a name="defunsk">A Beginner's Guide to Reasoning about Quantification in ACL2</a></H1>

<big><a href="http://www.cs.utexas.edu/~sandip">S. Ray</a></big>
<p>
Original Date: February 14, 2005.<br>  
Last Updated: November 9, 2008.<br> 
<br>
<br>

<small>
[<b>Note:</b> This version includes some of the changes to the
original documentation made by <a
href="http://www.cs.utexas.edu/~kaufmann">Matt Kaufmann</a> to
incorporate this tutorial into the ACL2 User's Manual.  Click <a
href="http://www.cs.utexas.edu/~moore/acl2/current/QUANTIFIER-TUTORIAL.html">here</a>
for the version in the User's Manual.]</small>

<br>
<br>
<hr>

<br>

This is a collection of notes that I wrote to remind myself of how to reason
about quantifiers when I just started.  Most users after they have gotten a
hang of quantifiers probably will not need this and will be able to have their
intuitions to guide them in the process.  But since many ACL2 users are not
used to quantification, I am hoping that this set of notes might help them to
think clearly while reasoning about quantifiers in ACL2.

<p>

Many ACL2 papers start with the sentence ``ACL2 is a quantifier-free
first-order logic of recursive functions.''  It is true that the
<em>syntax</em> of ACL2 is quantifier-free; every formula is assumed
to be universally quantified over all free variables in the formula.
But the <em>logic</em> in fact does afford arbitrary first-order
quantification.  This is obtained in ACL2 using a construct called
<code>defun-sk</code>.  See <a href="DEFUN-SK.html">defun-sk</a>.<p>

Many ACL2 users do not think in terms of <a
href="QUANTIFIERS.html">quantifiers</a>.  The focus is almost always
on defining recursive functions and reasoning about them using
induction.  That is entirely justified, in fact, since proving
theorems about recursive functions by induction plays to the strengths
of the theorem prover.  Nevertheless there are situations where it is
reasonable and often useful to think in terms of quantifiers.
However, reasoning about quantifiers requires that you get into the
mindset of thinking about theorems in terms of quantification.  This
note is about how to do this effectively given ACL2's implementation
of quantification.  This does not discuss <code><a
href="DEFUN-SK.html">defun-sk</a></code> in detail, but merely shows
some examples.  A detailed explanation of the implementation is in the
ACL2 <a href="DOCUMENTATION.html">documentation</a> (see <a
href="DEFUN-SK.html">defun-sk</a>); also see <a
href="CONSERVATIVITY-OF-DEFCHOOSE.html">conservativity-of-defchoose</a>.<p>

<b>Note:</b> Quantifiers can be used for some pretty cool things in
ACL2.  Perhaps the most interesting example is the way of using
quantifiers to introduce arbitrary tail-recursive equations; see the
paper <a
href="http://www.ccs.neu.edu/~pete/research/jar-partial-functions-acl2.html">Partial
Functions in ACL2</a> by <a
href="http://www.ccs.neu.edu/~pete">Panagiotis Manolios</a> and <a
href="http://www.cs.utexas.edu/~moore">J Strother Moore</a>.  This
note does not address applications of quantifiers, but merely how you
would reason about them once you think you want to use them.
<p>
Assume that you have some function <code>P</code>.  I have just left <code>P</code> as a unary
function stub below, since I do not care about what <code>P</code> is.

<pre>
(defstub P (*) =&gt; *)
</pre>

Now suppose you want to specify the concept that ``there exists some <code>x</code>
such that <code>(P x)</code> holds''.  ACL2 allows you to write that directly using
quantifiers.

<pre>
(defun-sk exists-P () (exists x (P x)))
</pre>

If you submit the above form in ACL2 you will see that the theorem prover
specifies two functions <code>exists-p</code> and <code>exists-p-witness</code>, and exports
the following constraints:

<pre>
1.  (defun exists-P () (P (exists-P-witness)))
2.  (defthm exists-P-suff (implies (p x) (exists-p)))
</pre>

Here <code>exists-P-witness</code> is a new function symbol in the current ACL2
theory.  What do the constraints above say?  Notice the constraint
<code>exists-p-suff</code>.  It says that if you can provide any <code>x</code> such that
<code>(P x)</code> holds, then you know that <code>exists-p</code> holds.  Think of the other
constraint (definition of <code>exists-p</code>) as going the other way.  That is, it
says that if <code>exists-p</code> holds, then there is some <code>x</code>, call it
<code>(exists-p-witness)</code>, for which <code>P</code> holds.  Notice that nothing else is
known about <code>exists-p-witness</code> than the two constraints above.<p>

[Note: <code>exists-p-witness</code> above is actually defined in ACL2 using a special
form called <code>defchoose</code>.  See <a href="DEFCHOOSE.html">defchoose</a>.  This note does not talk about
<code>defchoose</code>.  So far as this note is concerned, think of
<code>exists-p-witness</code> as a new function symbol that has been generated somehow
in ACL2, about which nothing other than the two facts above is known.]<p>

Similarly, you can talk about the concept that ``for all <code>x</code> <code>(P x)</code>
holds.'' This can be specified in ACL2 by the form:

<pre>
(defun-sk forall-P () (forall x (P x)))
</pre>

This produces the following two constraints:

<pre>
1.  (defun forall-P () (P (forall-p-witness)))
2.  (defthm forall-p-necc (implies (not (P x)) (not (forall-p))))
</pre>

To understand these, think of <code>for-all-p-witness</code> as producing some <code>x</code>
which does not satisfy <code>P</code>, if such a thing exists.  The constraint
<code>forall-p-necc</code> merely says that if <code>forall-p</code> holds then <code>P</code> is
satisfied for every <code>x</code>.  (To see this more clearly, just think of the
contrapositive of the formula shown.) The other constraint (definition of
<code>forall-p</code>) implies that if <code>forall-p</code> does not hold then there is some
<code>x</code>, call it <code>(forall-p-witness)</code>, which does not satisfy <code>P</code>.  To see
this, just consider the following formula which is immediately derivable from
the definition.

<pre>
(implies (not (forall-p)) (not (P (forall-witness))))
</pre>

The description above suggests that to reason about quantifiers, the
following Rules of Thumb, familiar to most any student of logic, are useful.

<blockquote>
<b>RT1:</b> To prove <code>(exists-p)</code>, construct some object
<code>A</code> such that <code>P</code> holds for <code>A</code> and
then use <code>exists-P-suff</code>.<p>

<b>RT2:</b> If you assume <code>exists-P</code> in your hypothesis,
use the definition of <code>exists-p</code> to know that
<code>P</code> holds for <code>exists-p-witness</code>.  To use this
to prove a theorem, you must be able to derive the theorem based on
the hypothesis that <code>P</code> holds for something, whatever the
something is.<p>

<b>RT3:</b> To prove <code>forall-P</code>, prove the theorem <code>(P
x)</code> (that is, that <code>P</code> holds for an arbitrary
<code>x</code>), and then simply instantiate the definition of
<code>forall-p</code>, that is, show that <code>P</code> holds for the
witness.<p>

<b>RT4:</b> If you assume <code>forall-p</code> in the hypothesis of
the theorem, see how you can prove your conclusion if indeed you were
given <code>(P x)</code> as a theorem.  Possibly for the conclusion to
hold, you needed that <code>P</code> holds for some specific set of
<code>x</code> values.  Then use the theorem
<code>forall-p-necc</code> by instantiating it for the specific
<code>x</code> values you care about.</blockquote>
<p>

Perhaps the above is too terse.  In the remainder of the note, we will
consider several examples of how this is done to prove theorems in ACL2 that
involve quantified notions.<p>

Let us consider two trivial theorems.  Assume that for some unary
function <code>r</code>, you have proved <code>(r x)</code> as a
theorem.  Let us see how you can prove that (1) there exists some x
such that <code>(r x)</code> holds, and (2) for all <code>x</code>
<code>(r x)</code> holds.<p>

We first model these things using <code><a
href="DEFUN-SK.html">defun-sk</a></code>.  Below, <code>r</code> is
simply some function for which <code>(r x)</code> is a theorem.

<pre>
(encapsulate 
 (((r *) =&gt; *))
 (local (defun r (x) (declare (ignore x)) t))
 (defthm r-holds (r x)))<p>

(defun-sk exists-r () (exists x (r x)))
(defun-sk forall-r () (forall x (r x)))
</pre>

ACL2 does not have too much reasoning support for quantifiers.  So in
most cases, one would need <code>:use</code> hints to reason about
quantifiers.  In order to apply <code>:use</code> <a
href="HINTS.html">hints</a>, it is preferable to keep the function
definitions and theorems disabled.

<pre>
(in-theory (disable exists-r exists-r-suff forall-r forall-r-necc))
</pre>

Let us now prove that there is some <code>x</code> such that <code>(r
x)</code> holds.  Since we want to prove <code>exists-r</code>, we
must use <code>exists-r-suff</code> by RT1.  We do not need to
construct any instance here since <code>r</code> holds for all
<code>x</code> by the theorem above.

<pre>
(defthm exists-r-holds
  (exists-r)
  :hints (("Goal" :use ((:instance exists-r-suff)))))
</pre>

Let us now prove the theorem that for all <code>x</code>, <code>(r
x)</code> holds.  By RT3, we must be able to prove it by definition of
<code>forall-r</code>.

<pre>
(defthm forall-r-holds
  (forall-r)
  :hints (("Goal" :use ((:instance (:definition forall-r))))))
</pre>

<b>Note:</b> Probably no ACL2 user in his or her right mind would
prove the theorems <code>exists-r-holds</code> and
<code>forall-r-holds</code> above.  The theorems shown are only for
demonstration purposes.<p>

For the remainder of this note we will assume that we have two stubbed
out unary functions <code>M</code> and <code>N</code>, and we will
look at proving some quantified properties of these functions.

<pre>
(defstub M (*) =&gt; *)
(defstub N (*) =&gt; *)
</pre>

Let us now define the predicates <code>all-M</code>,
<code>all-N</code>, <code>ex-M</code>, and <code>ex-N</code>
specifying the various quantifications.

<pre>
(defun-sk all-M () (forall x (M x)))
(defun-sk all-N () (forall x (N x)))
(defun-sk some-M () (exists x (M x)))
(defun-sk some-N () (exists x (N x)))<p>

(in-theory (disable all-M all-N all-M-necc all-N-necc))
(in-theory (disable some-M some-N some-M-suff some-N-suff))
</pre>

Let us prove the classic distributive properties of quantification:
the distributivity of universal quantification over conjunction, and
the distributivity of existential quantification over disjunction.  We
can state these properties informally in ``pseudo ACL2'' notation as
follows:

<pre>
1.  (exists x: (M x)) or (exists x: (N x)) &lt;=&gt; (exists x: (M x) or (N x))
2.  (forall x: (M x)) and (forall: x (N x)) &lt;=&gt; (forall x: (M x) and (N x))
</pre>

To make these notions formal we of course need to define the formulas
at the right-hand sides of 1 and 2.  So we define <code>some-MN</code>
and <code>all-MN</code> to capture these concepts.

<pre>
(defun-sk some-MN () (exists x (or (M x) (N x))))
(defun-sk all-MN () (forall x (and (M x) (N x))))<p>

(in-theory (disable all-MN all-MN-necc some-MN some-MN-suff))
</pre>

First consider proving property 1.  The formal statement of this
theorem would be: <code>(iff (some-MN) (or (some-M)
(some-N)))</code>.<p>

How do we prove this theorem?  Looking at RT1-RT4 above, note that
they suggest how one should reason about quantification when one has
an ``implication''.  But here we have an ``equivalence''.  This
suggests another rule of thumb.

<blockquote>
<b>RT5:</b> Whenever possible, prove an equivalence involving
quantifiers by proving two implications.</blockquote>
<p>

Let us apply RT5 to prove the theorems above.  So we will first prove:
<code>(implies (some-MN) (or (some-M) (some-N)))</code><p>

How can we prove this?  This involves assuming a quantified predicate
<code>(some-MN)</code>, so we must use RT2 and apply the definition of <code>some-MN</code>.
Since the conclusion involves a disjunction of two quantified predicates, by
RT1 we must be able to construct two objects <code>A</code> and <code>B</code> such that either
<code>M</code> holds for <code>A</code> or <code>N</code> holds for <code>B</code>, so that we can then invoke
<code>some-M-suff</code> and <code>some-N-suff</code> to prove the conclusion.  But now notice
that if <code>some-MN</code> is true, then there is already an object, in fact
<code>some-MN-witness</code>, such that either <code>M</code> holds for it, or <code>N</code> holds for
it.  And we know this is the case from the definition of <code>some-MN</code>! So we
will simply prove the theorem instantiating <code>some-M-suff</code> and
<code>some-N-suff</code> with this witness.  The conclusion is that the following
event will go through with ACL2.

<pre>
(defthm le1
  (implies (some-MN)
           (or (some-M) (some-N)))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition some-MN))
                  (:instance some-M-suff
                             (x (some-MN-witness)))
                  (:instance some-N-suff
                             (x (some-MN-witness)))))))
</pre>

This also suggests the following rule of thumb:

<blockquote>
<b>RT6:</b> If a conjecture involves assuming an existentially
quantified predicate in the hypothesis from which you are trying to
prove an existentially quantified predicate, use the witness of the
existential quantification in the hypothesis to construct the witness
for the existential quantification in the conclusion.</blockquote>
<p>

Let us now try to prove the converse of le1, that is:
<code>(implies (or (some-M) (some-N)) (some-MN))</code><p>

Since the hypothesis is a disjunction, we will just prove each case
individually instead of proving the theorem by a :<code>cases</code>
hint.  So we prove the following two lemmas.

<pre>
(defthm le2
  (implies (some-M) (some-MN))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition some-M))
                  (:instance some-MN-suff
                             (x (some-M-witness)))))))<p>

(defthm le3
  (implies (some-N) (some-MN))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition some-N))
                  (:instance some-MN-suff
                             (x (some-N-witness)))))))
</pre>

Note that the hints above are simply applications of RT6 as in
<code>le1</code>.  With these lemmas, of course the main theorem is
trivial.

<pre>
(defthmd |some disjunction|
  (iff (some-MN) (or (some-M) (some-N)))
  :hints (("Goal"
            :use ((:instance le1)
                  (:instance le2)
                  (:instance le3)))))
</pre>

Let us now prove the distributivity of universal quantification over
conjunction, that is, the formula: <code>(iff (all-MN) (and (all-M)
(all-N)))</code><p>

Applying RT5, we will again decompose this into two implications.  So
consider first the one-way implication: <code>(implies (and (all-M)
(all-N)) (all-MN))</code>.<p>

Here we get to assume <code>all-M</code> and <code>all-N</code>.  Thus
by RT4 we can use <code>all-M-necc</code> and <code>all-N-necc</code>
to think as if we are given the formulas <code>(M x)</code> and
<code>(N x)</code> as theorems.  The conclusion here is also a
universal quantification, namely we have to prove <code>all-MN</code>.
Then RT3 tells us to proceed as follows.  Take any object
<code>y</code>.  Try to find an instantiation <code>z</code> of the
hypothesis that implies <code>(and (M y) (N y))</code>.  Then
instantiate <code>y</code> with <code>all-MN-witness</code>.  Note
that the hypothesis lets us assume <code>(M x)</code> and <code>(N
x)</code> to be theorems.  Thus to justify we need to instantiate
<code>x</code> with <code>y</code>, and in this case, therefore, with
<code>all-MN-witness</code>.  To make the long story short, the
following event goes through with ACL2:

<pre>
(defthm lf1
   (implies (and (all-M) (all-N))
            (all-MN))
    :rule-classes nil
    :hints (("Goal"
              :use ((:instance (:definition all-MN))
                    (:instance all-M-necc (x (all-MN-witness)))
                    (:instance all-N-necc (x (all-MN-witness)))))))
</pre>

This suggests the following rule of thumb which is a dual of RT6:

<blockquote>
<b>RT7:</b> If a conjecture assumes some universally quantified
predicate in the hypothesis and its conclusion asserts a universallly
quantified predicate, then instantiate the ``necessary condition''
(<code>forall-mn-necc</code>) of the hypothesis with the witness of
the conclusion to prove the conjecture.</blockquote>
<p>

Applying RT7 now we can easily prove the other theorems that we need
to show that universal quantification distributes over conjunction.
Let us just go through this motion in ACL2.

<pre>
(defthm lf2
  (implies (all-MN)
           (all-M))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition all-M))
                  (:instance all-MN-necc 
                             (x (all-M-witness)))))))<p>

(defthm lf3
  (implies (all-MN)
           (all-N))
  :rule-classes nil      
  :hints (("Goal"
            :use ((:instance (:definition all-N))
                  (:instance all-MN-necc 
                             (x (all-N-witness)))))))<p>

(defthmd |all conjunction|
  (iff (all-MN)
       (and (all-M) (all-N)))
 :hints (("Goal" :use ((:instance lf1)
                       (:instance lf2)
                       (:instance lf3)))))
</pre>

The rules of thumb for universal and existential quantification should
make you realize the duality of their use.  Every reasoning method
about universal quantification can be cast as a way of reasoning about
existential quantification, and vice versa.  Whether you reason using
universal and existential quantifiers depends on what is natural in a
particular context.  But just for the sake of completeness let us
prove the duality of universal and existential quantifiers.  So what
we want to prove is the following:

<pre>
3.  (forall x (not (M x))) = (not (exists x (M x)))
</pre>

We first formalize the notion of <code>(forall x (not (M x)))</code> as a
quantification.

<pre>
(defun-sk none-M () (forall x (not (M x))))
(in-theory (disable none-M none-M-necc))
</pre>

So we now want to prove: <code>(equal (none-M) (not
(some-M)))</code>.<p>

As before, we should prove this as a pair of implications.  So let us
prove first: <code>(implies (none-M) (not (some-M)))</code>.<p>

This may seem to assert an existential quantification in the
conclusion, but rather, it asserts the <em>negation</em> of an
existential quantification.  We are now trying to prove that something
does not exist.  How do we do that?  We can show that nothing
satisfies <code>M</code> by just showing that
<code>(some-M-witness)</code> does not satisfy <code>M</code>.  This
suggests the following rule of thumb:

<blockquote>
<b>RT8:</b> When you encounter the negation of an existential
quantification think in terms of a universal quantification, and
vice-versa.  </blockquote>

Ok, so now applying RT8 and RT3 you should be trying to apply the
definition of <code>some-M</code>.  The hypothesis is just a pure
(non-negated) universal quantification so you should apply RT4.  A
blind application lets us prove the theorem as below.

<pre>
(defthm nl1
  (implies (none-M) (not (some-M)))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition some-M))
                  (:instance none-M-necc (x (some-M-witness)))))))
</pre>

How about the converse implication?  I have deliberately written it as
<code>(implies (not (none-M)) (some-M))</code> instead of switching
the left-hand and right-hand sides of <code>nl1</code>, which would
have been equivalent.  Again, RH8 tells us how to reason about it, in
this case using RH2, and we succeed.

<pre>
(defthm nl2
  (implies (not (none-M)) (some-M))
  :rule-classes nil
  :hints (("Goal"
            :use ((:instance (:definition none-M))
                  (:instance some-M-suff (x (none-M-witness)))))))
</pre>

So finally we just go through the motions of proving the equality.

<pre>
(defthmd |forall not = not exists|
  (equal (none-M) (not (some-M)))
  :hints (("Goal"
            :use ((:instance nl1)
                  (:instance nl2)))))
</pre>

Let us now see if we can prove a slightly more advanced theorem which
can be stated informally as: If there is a natural number
<code>x</code> which satisfies <code>M</code>, then there is a least
natural number <code>y</code> that satisfies <code>M</code>.<p>

<b>Note:</b> Any time I have had to reason about existential
quantification I have had to do this particular style of reasoning and
state that if there is an object satisfying a predicate, then there is
also a ``minimal'' object satisfying the predicate.]<p>

Let us formalize this concept.  We first define the concept of
existence of a natural number satisfying <code>x</code>.

<pre>
(defun-sk some-nat-M () (exists x (and (natp x) (M x)))) (in-theory
(disable some-nat-M some-nat-M-suff)) </pre>

We now talk about what it means to say that <code>x</code> is the
least number satisfying <code>M</code>.

<pre>
(defun-sk none-below (y) 
  (forall r (implies (and (natp r) (&lt; r y)) (not (M r))))))
(in-theory (disable none-below none-below-necc))<p>

(defun-sk min-M () (exists y (and (M y) (natp y) (none-below y))))
(in-theory (disable min-M min-M-suff))
</pre>

The predicate <code>none-below</code> says that no natural number less
than <code>y</code> satisfies <code>M</code>.  The predicate
<code>min-M</code> says that there is some natural number
<code>y</code> satisfying <code>M</code> such that
<code>none-below</code> holds for <code>y</code>.<p>

So the formula we want to prove is: <code>(implies (some-nat-M) (min-M))</code>.<p>

Since the formula requires that we prove an existential
quantification, RT1 tells us to construct some object satisfying the
predicate over which we are quantifying.  We should then be able to
instantiate <code>min-M-suff</code> with this object.  That predicate
says that the object must be the least natural number that satisfies
<code>M</code>.  Since such an object is uniquely computable if we
know that there exists some natural number satisfying <code>M</code>,
let us just write a recursive function to compute it.  This function
is <code>least-M</code> below.

<pre>
(defun least-M-aux (i bound)
  (declare (xargs :measure (nfix (- (1+ bound) i))))
  (cond ((or (not (natp i))
             (not (natp bound))
             (&gt; i bound))
         0)
       ((M i) i)
       (t (least-M-aux (+ i 1) bound))))<p>

(defun least-M (bound) (least-M-aux 0 bound))
</pre>

Let us now reason about this function as one does typically.  So we
prove that this object is indeed the least natural number that
satisfies <code>M</code>, assuming that <code>bound</code> is a
natural number that satisfies <code>M</code>.

<pre>
(defthm least-aux-produces-an-M
  (implies (and (natp i)
                (natp bound)
                (&lt;= i bound)
                (M bound))
           (M (least-M-aux i bound))))<p>

(defthm least-&lt;=bound
  (implies (&lt;= 0 bound)
           (&lt;= (least-M-aux i bound) bound)))<p>

(defthm least-aux-produces-least
  (implies (and (natp i)
                (natp j)
                (natp bound)
                (&lt;= i j)
                (&lt;= j bound)
                (M j))
            (&lt;= (least-M-aux i bound) j)))<p>

(defthm least-aux-produces-natp 
  (natp (least-M-aux i bound)))<p>

(defthmd least-is-minimal-satisfying-m
  (implies (and (natp bound)
                (natp i)
                 (&lt; i (least-M bound)))
           (not (M i)))
  :hints (("Goal"
            :in-theory (disable least-aux-produces-least least-&lt;=bound)
            :use ((:instance least-&lt;=bound
                             (i 0))
                  (:instance least-aux-produces-least
                             (i 0)
                             (j i))))))<p>

(defthm least-has-m 
  (implies (and (natp bound) 
		  (m bound)) 
	     (M (least-M bound))))<p>

(defthm least-is-natp 
  (natp (least-M bound)))
</pre>

So we have done that, and hopefully this is all that we need about
<code>least-M</code>.  So we disable everything.

<pre>
(in-theory (disable least-M natp))
</pre>

Now of course we note that the statement of the conjecture we are
interested in has two quantifiers, an inner <code>forall</code> (from
<code>none-below</code>) and an outer <code>exists</code> (from
<code>min-M</code>).  Since ACL2 is not very good with quantification,
we hold its hands to reason with the quantifier part.  So we will
first prove something about the <code>forall</code> and then use it to
prove what we need about the <code>exists</code>.

<blockquote>
<b>RT9:</b> When you face nested quantifiers, reason about each
nesting separately.</blockquote>
<p>

So what do we want to prove about the inner quantifier?  Looking
carefully at the definition of <code>none-below</code> we see that it
is saying that for all natural numbers <code>r</code> &lt;
<code>y</code>, <code>(M r)</code> does not hold.  Well, how would we
want to use this fact when we want to prove our final theorem?  We
expect that we will instantiate <code>min-M-suff</code> with the
object <code>(least-M bound)</code> where we know (via the outermost
existential quantifier) that <code>M</code> holds for
<code>bound</code>, and we will then want to show that
<code>none-below</code> holds for <code>(least-M bound)</code>.  So
let us prove that for any natural number (call it <code>bound</code>),
<code>none-below</code> holds for <code>(least-M bound)</code>.  For
the final theorem we only need it for natural numbers satisfying
<code>M</code>, but note that from the lemma
<code>least-is-minimal-satisfying-m</code> we really do not need that
<code>bound</code> satisfies <code>M</code>.<p>

So we are now proving:
<code>(implies (natp bound) (none-below (least-M bound)))</code>.<p>

Well since this is a standard case of proving a universally quantified
predicate, we just apply RT3.  We have proved that for all naturals
<code>i</code> &lt; <code>(least-M bound)</code>, <code>i</code> does
not satisfy <code>M</code> (lemma
<code>least-is-minimal-satisfying-M</code>), so we merely need the
instantiation of that lemma with <code>none-below-witness</code> of
the thing we are trying to prove, that is, <code>(least-M
bound)</code>.  The theorem below thus goes through.

<pre>
(defthm least-is-minimal
  (implies (natp bound)
           (none-below (least-M bound)))
  :hints (("Goal"
            :use ((:instance (:definition none-below)
                             (y (least-M bound)))
                  (:instance least-is-minimal-satisfying-m
                             (i (none-below-witness (least-M bound))))))))
</pre>

Finally we are in the outermost existential quantifier, and are in the
process of applying <code>min-M-suff</code>.  What object should we
instantiate it with?  We must instantiate it with <code>(least-M
bound)</code> where <code>bound</code> is an object which must satisfy
<code>M</code> and is a natural.  We have such an object, namely
<code>(some-nat-M-witness)</code> which we know have all these
qualities given the hypothesis.  So the proof now is just RT1 and RT2.

<pre>
(defthm |minimal exists|
  (implies (some-nat-M) (min-M))
  :hints (("Goal"
            :use ((:instance min-M-suff
                             (y (least-M (some-nat-M-witness))))
                  (:instance (:definition some-nat-M))))))
</pre>
<p>

If you are comfortable with the reasoning above, then you are
comfortable with quantifiers and probably will not need these notes
any more.  In my opinion, the best way of dealing with ACL2 is to ask
yourself why you think something is a theorem, and the rules of thumb
above are simply guides to the questions that you need to ask when you
are dealing with quantification.<p>

Here is just a simple exercise for you to test if you understand the
reasoning process.<p>

Exercise:<p>

In the example above we showed that if there exists a natural number
<code>x</code> satisfying <code>M</code> then there is another natural
number <code>y</code> such that <code>y</code> satisfies
<code>M</code> and for every natural number <code>z</code> &lt;
<code>y</code>, <code>z</code> does not.  What would happen if we
remove the restriction of <code>x</code>, <code>y</code>, and
<code>z</code> being naturals?  Of course, we will not talk about
<code>&lt;</code> any more, but suppose you use the total order on all
ACL2 objects (from <code>"books/misc/total-order"</code>).  More
concretely, consider the definition of <code>some-M</code> above.  Let
us now define two other functions:

<pre>
(include-book "misc/total-order" :dir :system)<p>

(defun-sk none-below-2 (y)
  (forall r (implies (&lt;&lt; r y) (not (M r)))))<p>

(defun-sk min-M2 () (exists y (and (M y) (none-below-2 y))))
</pre>

The question is whether <code>(implies (some-M) (min-M2))</code> is a
theorem.  Can you prove it?  Can you disprove it?  <br>

<br>
<br>

 </body> </html>
